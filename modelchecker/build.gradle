buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}
import org.apache.tools.ant.taskdefs.condition.Os

static getNatives() {
    String ret = "../resources/native/"
    if (System.getProperty("os.arch").endsWith("64")) ret += "amd64"
    if (System.getProperty("os.arch").endsWith("86")) ret += "x86"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ret+="-windows"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        ret+="-macosx"
    } else {
        ret +="-linux"
    }
    return ret
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id "com.moowork.node" version "1.2.0"
}

group 'net.modelchecker'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
shadowJar {
    baseName = 'ModelChecker'
    classifier = null
    version = null
    destinationDir = new File("../")
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    version = '1.0'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}
subprojects {
    apply plugin: 'java'
    apply from: "$rootDir/gradle/version.gradle"
    apply plugin: "com.github.johnrengelman.shadow"

    repositories { jcenter() }
    dependencies {
        if (project.name != "util") {
            compile project(":util")
        }
        testCompile group: 'junit', name: 'junit', version: '4.11'
        compileOnly 'org.projectlombok:lombok:1.16.12'
        compile 'uk.com.robust-it:cloning:1.9.3'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    }
    sourceCompatibility = 1.8

    test {
        //TODO: Edit this to make it work when we change the resource directory
        workingDir new File("../resources/")
        testLogging {
            showStandardStreams = true
        }
        systemProperty "java.library.path", natives
        environment "PATH", natives
        environment "LD_LIBRARY_PATH", natives
        //TODO: We need to use a specific lib dir on mac.
        environment "DYLD_LIBRARY_PATH", natives
    }
}




node {
    // Version of node to use.
    version = '6.11.0'

    // Version of npm to use.
    npmVersion = '5.1.0'

    // Version of Yarn to use.
    yarnVersion = '0.27.5'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/npm")

    // Set the work directory for Yarn
    yarnWorkDir = file("${project.buildDir}/yarn")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/../site/app")
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    def osName = System.getProperty("os.name").toLowerCase()
    if (osName.contains("windows")) {
        script = project.file("${project.projectDir}/../site/app/node_modules/webpack/bin/webpack.js")
    } else {
        script = project.file("${project.projectDir}/../site/app/node_modules/.bin/webpack")
    }
    execOverrides {
        it.workingDir = "${project.projectDir}/../site/app"
    }
}

processResources.dependsOn 'webpack'

test.dependsOn 'cleanTest'

tasks.eclipse.doLast {
    delete '.project'
}

//mainClassName = 'mc.Main'
