buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}
import org.apache.tools.ant.taskdefs.condition.Os

static getNatives() {
    String ret = '../resources/native/'
    if (System.getProperty("os.arch").endsWith("64")) ret += "amd64"
    if (System.getProperty("os.arch").endsWith("86")) ret += "x86"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ret+="-windows"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        ret+="-macosx"
    } else {
        ret +="-linux"
    }
    return ret
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'com.moowork.node' version '1.2.0'
}

group   'net.modelchecker'
version '1.0-SNAPSHOT'

apply plugin:   'java'
apply plugin:   'application'
apply plugin:   'com.github.johnrengelman.shadow'
mainClassName = 'mc.Main'
shadowJar {
    baseName = 'ModelChecker'
    classifier = null
    version = null
    destinationDir = new File("../")
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    version = '1.0'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}
subprojects {
    apply plugin: 'java'
    apply from: "$rootDir/gradle/version.gradle"

    dependencies {
        if (project.name !=  'util') {
            compile project(':util')
        }
        testCompile 'junit:junit:4.11'
        compileOnly 'org.projectlombok:lombok:1.16.12'
        compile     'uk.com.robust-it:cloning:1.9.3'
        compile     'org.apache.commons:commons-lang3:3.5'
        compile     'commons-io:commons-io:2.5'
    }

    sourceCompatibility = 1.8

    test {
        workingDir new File("../resources/")
        testLogging {
            showStandardStreams = true
        }
        systemProperty 'java.library.path', natives
        environment    'PATH',              natives
        environment    'LD_LIBRARY_PATH',   natives
        environment    'DYLD_LIBRARY_PATH', natives
    }
}

test {
    workingDir new File("resources/")
    testLogging {
        showStandardStreams = true
    }
    systemProperty 'java.library.path', natives
    environment    'PATH',              natives
    environment    'LD_LIBRARY_PATH',   natives
    environment    'DYLD_LIBRARY_PATH', natives
}

dependencies {
    compile project(':ast')
    compile project(':automata_model')
    compile project(':compiler')
    compile project(':core')
    compile project(':evaluator')
    compile project(':interpreter')
    compile project(':parsing')
    compile project(':util')
    compile project(':webserver')
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "../site"
            exclude '**/node_modules/**','**/scripts/**','**/yarn.lock','**/webpack.config.js','**/package.json'
        }
    }
}

node {
    // Version of node to use.
    version = '6.11.0'

    // Version of npm to use.
    npmVersion = '5.1.0'

    // Version of Yarn to use.
    yarnVersion = '0.27.5'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/npm")

    // Set the work directory for Yarn
    yarnWorkDir = file("${project.buildDir}/yarn")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/../site/app")
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    def osName = System.getProperty("os.name").toLowerCase()
    if (osName.contains("windows")) {
        script = project.file("${project.projectDir}/../site/app/node_modules/webpack/bin/webpack.js")
    } else {
        script = project.file("${project.projectDir}/../site/app/node_modules/.bin/webpack")
    }
    execOverrides {
        it.workingDir = "${project.projectDir}/../site/app"
    }
}

processResources.dependsOn 'webpack'
test.dependsOn 'cleanTest'
assemble.dependsOn shadowJar

tasks.eclipse.doLast {
    delete '.project'
}


