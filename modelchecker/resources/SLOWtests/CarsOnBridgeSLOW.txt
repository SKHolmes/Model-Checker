/*     Reworking Abrials Cars on a bridge in process algebra
Establishes liveness properties for a finite state approximation.
*/
const Max = 1.
const Cnum = 1.
processes {
/* 
   model that clearly satisfies the  Max number of cars
   Assumes System sees how many cars are one the island
*/
M1 =  C[0],
  C[i:0..Max] = (when (i < Max) ml2is -> C[i+1]
                | when (i > 0) is2ml -> C[i-1]).

/*
   New model that satisfies one way bridge
   Assumes System sees: 1. how many cars are one the island
            2. when a car is on the bridge
*/
M2 = C[0][0][0],
 C[mlis:0..Max][is:0..Max][isml:0..Max] =
  (when((isml == 0) && ((mlis + is) < Max))    ml2br->C[mlis+1][is][isml]
  |when((mlis>0)  && (is<Max))                 br2is->C[mlis-1][is+1][isml]
  |when((mlis == 0) && (is > 0) && (isml<Max)) is2br->C[mlis][is-1][isml+1]
  | when (isml>0)                              br2ml->C[mlis][is][isml-1]).


// note because a global perspective is taken complete trace semantics is used
 M2to1 = simp (nfa2dfa(obs (M2/{ml2is/ml2br,is2ml/br2ml}\{br2is,is2br}))). 

// used to show the need for complete trace semantics not failure semantics 
 M2bis1 = simp (obs (M2/{ml2is/ml2br,is2ml/br2ml}\{br2is,is2br})). 
}
operation{
M1 ~ M2to1.    // M2 is a complete trace refinement of M1
M1 !~ M2bis1. // NOT a failure refinement
}

processes {
/*
  Introduce trafic lights so car only moves when trafic light is green.
      Similar assumptions as M2
*/
TLred = (red->TLred | turnGreen->TLgreen),
   TLgreen = (green-> TLgreen | turnRed -> TLred).
TLgreen = (mlgreen-> TLgreen | mlturnRed -> TLred),
    TLred = (mlred->TLred | mlturnGreen->TLgreen).

M3 = (TLgreen / {ml2br/mlgreen} || 
          TLred   /{is2br/green}  ||
            M2 ) \{turnRed,turnGreen,red,
                       mlturnRed,mlturnGreen,mlred}.

M3to1 = simp(nfa2dfa(obs(M3/{ml2is/ml2br,is2ml/br2ml}\{br2is,is2br}))).

M3to2 = simp(nfa2dfa(obs(M3))).
}
operation{
M1 ~ M3to1.   // sanity check - debugging step
M2 ~ M3to2.  //M3 is a complete trace refinement of M2
}

processes {
/* only one car at a time can cross one sensor 
   the seeOn event allows the controller to test the sensor
   alternativly an additional state could be added to the controller
*/
//Sensor = ([j:1..Cnum].seeOff -> Sensor |([j].on -> [j].off->Sensor)).
Sen = ([j:1..Cnum]on -> [j]off->Sen).
        
Sensor = forall[s:1..4] ((on[s][j:1..Cnum] -> off[s][j]->Sensor[s])).
/*
many cars can see the green light  but cars do not all need to see it
red only needed to control turning the island traffic light green.
*/

TL = ( red[j:1..Cnum]->TL |turnGreen->TLgx),  //  [j:1..Cnum].red->TL |
   TLgx = (green[j:1..Cnum]-> TLgx | turnRed -> TL). 

TLg = (mlgreen[j:1..Cnum]-> TLg | mlturnRed -> TLx),    
  TLx = ( mlred[j:1..Cnum]->TLx |mlturnGreen->TLg).    // [j:1..Cnum].red->TLx |
  
/* To force the car to move quickly of the sensor when it sees the light is green
   we rename the  "off" events.
*/
TLml = TLg /{off[1][i:1..Cnum]/mlgreen[i]}.
TLis = TL /{off[3][i:1..Cnum]/green[i]}. 


/*
The sensors are input to the controller that
ml2br = Sensor[1]
br2is = Sensor[2]
is2br = Sensor[3]
br2ml = Sensor[4]
    1.  always responds to sensor information
    2.  only switches the lights when safe to do so
Drivers move as soon as they see a green light!  If the driver waits, 
    and allows the lights to change color, then moves off with out checking the light
    they will crash.
    a) Tl(green) sync with Car(green)  
    b) rename green to sen.off  so that the Car moves promptly 
    Error 1.  Cars not obeying one way bridge  will crash
    Error 2.  Overfill the bridge+island report failMax
    
 The use of ranges 0..Max+1 means the controller will allow the system to fail 
 if the cars do not obey the trafic lights!
 
 */
 Controller = C[0][0][0][0][0],
C[mlis:0..Max+1][is:0..Max+1][isml:0..Max+1][mls:0..1][iss:0..1] =
 ( when (mlis +isml+ is == Max) mlturnRed->C[mlis][is][isml][mls][iss]
| when((mlis ==0) && (iss == 1))  mlturnRed->turnGreen->C[mlis][is][isml][mls][iss]
| when((mlis ==0) && (iss == 1))  [i:1..Cnum]mlred->turnGreen->C[mlis][is][isml][mls][iss]

| when(isml==0 && (is + isml<Max) && mls==1) turnRed->
                                            mlturnGreen->C[mlis][is][isml][mls][iss]
 | when (mlis > 0 && isml> 0)       crash -> STOP
 | when (mlis < 0 || isml< 0)       imposible -> STOP
 | when (mlis +isml+ is > Max) failMax->STOP
 
|  when (mls == 0 && !((mlis ==0) && (is > 0) && iss == 0)) 
                             on[1][j:1..Cnum] -> C[mlis][is][isml][1][iss]
// the car only askes to go  off after seeing green and the controller turne the light
| when (mls==1 &&(mlis +isml+ is < (Max-1))) off[1][j:1..Cnum]->
                                     C[mlis+1][is][isml][0][iss]
| when (mls==1 &&(mlis +isml+ is == (Max-1))) off[1][j:1..Cnum]->mlturnRed->
                                     C[mlis+1][is][isml][0][iss]

// only one cart can be on the sensor at a time 

| when (mlis> 0 && is<=Max)  
       off[2][j:1..Cnum]->C[mlis-1][is+1][isml][mls][iss]                 
| when (iss == 0) on[3][j:1..Cnum] -> C[mlis][is][isml][mls][1]
| when (iss == 1 &&(is>0 && isml <= Max))   
                    off[3][j:1..Cnum]->C[mlis][is-1][isml+1][mls][0]
| when (isml > 0)   off[4][j:1..Cnum]->C[mlis][is][isml-1][mls][iss]

).

 // | when (mlis == Max)    [j:1..Cnum].ml2br ->failMLis->STOP
// | when (isml == Max)    [j:1..Cnum].is2br ->failISml->STOP

 /*  
 a Car controlls when the sensors is off and on 
 but waether a  Car moves off a sensor is controlled by the trafic lights
 */

Car =     on[1] -> mlgreen-> off[1] ->  Carbr2is,
       Carbr2is =  on[2] -> off[2] ->  Caris,
       Caris =   on[3] -> green-> off[3] -> Carbr2ml,
       Carbr2ml =  on[4] -> off[4]-> Car.
       

Cars = forall [c:1..Cnum]  (  on[1][c] -> mlgreen[c]-> off[1][c] ->  Carbr2is[c],
             Carbr2is[c] =   on[2][c] -> off[2][c] ->  Caris[c],
                Caris[c] =   on[3][c] -> green[c]-> off[3][c] -> Carbr2ml[c],
              Carbr2ml[c] =  on[4][c] -> off[4][c]-> Cars[c]).



M4 = (Controller ||  Sensor ||
         TLml || 
         TLis ||  Cars ).    

M4to2 = simp(nfa2dfa(obs(M4/{ //is2br/[i:1..Cnum].is2br, 
                           ml2br/off[1][i:1..Cnum],
                           br2ml/off[4][i:1..Cnum],
                           br2is/off[2][i:1..Cnum],
                           is2br/off[3][i:1..Cnum]
           } \{on[1][i:1..Cnum],
               on[2][i:1..Cnum],
               on[3][i:1..Cnum],
               on[4][i:1..Cnum],
               mlgreen[i:1..Cnum], mlred[i:1..Cnum],  mlturnRed,mlturnGreen,
              green[i:1..Cnum],  red[i:1..Cnum],  turnRed,turnGreen  
           }))).
   
M4to1 = simp(nfa2dfa(obs(M4to2/{ml2is/ml2br,is2ml/br2ml}\{br2is,is2br}))). 


}
//automata Car, Controller.
operation{
M1 ~ M4to1.  // sanity - debugging
M2 ~ M4to2.  // M4 is a complete trace refinement of M2
} 











